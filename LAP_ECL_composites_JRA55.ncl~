load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/wrf/WRFUserARW.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/esmf/ESMF_regridding.ncl"

ECLDIR="/srv/ccrc/data34/z3478332/ECLtracks/"
OUTDIR="/srv/ccrc/data37/z3478332/ECL_evaluation/"
syear=1988

source="JRA55" ;"ERAI-75-6"
res=50
rad="rad2cv1"
proj=240

; This will have to be done differently for ERAI and MERRA as different resolutions.
; And Ale didn't regrid them all. Lame. 
DATADIR="/srv/ccrc/data41/z3444417/Data/"+source+"/global/"

if(source.eq."JRA55") then
  lon0=fspan(-10.125,10.125,37)
  lon0!0="lon"
  lon0@units="degrees_E"
  lat0=fspan(-10.125,10.125,37)
  lat0!0="lat"
  lat0@units="degrees_N"

  fin=addfile(DATADIR+"psl/raw/JRA55_psl_2005_3.nc","r")
  lat1=fin->g4_lat_2
  lon1=fin->g4_lon_3
  delete(fin)
end if

  dir=ECLDIR+"outputUM_"+source+"_"+res+"_topo_"+rad+"_proj"+proj+"/"
  filename =dir+"ECLfixes_umelb_"+source+"_"+res+"_topo_"+rad+"_proj"+proj+".csv"
  read_data= asciiread(filename,-1,"string")
  header    = read_data(0)        ; Header. Use for variable names.
  data      = read_data(1:)       ; Get rid of first line which is a header.
  quote = inttochar(34) 
  delim=",:"+quote
  date1 = tointeger(str_get_field(data,4,delim))
  I=ind(date1.ge.syear*10000)
  date = tointeger(str_get_field(data(I),4,delim))
  time = tointeger(str_get_field(data(I),5,delim))
  lat = tofloat(str_get_field(data(I),9,delim))
  lon= tofloat(str_get_field(data(I),8,delim))
  len=dimsizes(date)  
  year=floor(date/10000.)
  month=mod(floor(date/100.),100)
  day=mod(date,100)
  delete(I)

; Output files

  ECL_U10=new((/len,dimsizes(lat0),dimsizes(lon0)/),"double")
  ECL_U10!0="Time"
  ECL_U10!1="lat"
  ECL_U10!2="lon"
  ECL_V10=ECL_U10
  ECL_SLP=ECL_U10
  ECL_U10@description="6-hour instantaneous 10m zonal wind within 1000km radius of low centre"
  ECL_V10@description="6-hour instantaneous 10m meridional wind within 1000km radius of low centre"
  ECL_SLP@description="6-hour instantaneous slp within 1000km radius of low centre"

  I=ind_nearest_coord(lat,lat1,0)
  J=ind_nearest_coord(lon,lon1,0)

 do ip=0,len-1
      if(mod(ip,100).eq.0) then
         print(date(ip))
      end if

       mnum=floor((month(ip)-1)/3)
 
       FILE1=addfile(DATADIR+"U10/raw/JRA55_U10_"+year(ip)+"_"+mnum+".nc","r")
       FILE2=addfile(DATADIR+"V10/raw/JRA55_V10_"+year(ip)+"_"+mnum+".nc","r")
       FILE3=addfile(DATADIR+"psl/raw/JRA55_psl_"+year(ip)+"_"+mnum+".nc","r")

       time2=FILE1->initial_time0_encoded
       n=ind(time2.eq.date(ip)*100+time(ip))

       u=FILE1->UGRD_GDS4_HTGL(n-1,1,:,:)
       v=FILE2->VGRD_GDS4_HTGL(n-1,1,:,:)
       p=FILE3->PRMSL_GDS4_MSL(n-1,1,:,:)/100
 
       if(I(ip).gt.20.and.I(ip).lt.300.and.J(ip).gt.20.and.J(ip).lt.620) then
         ECL_U10(ip,:,:)=u((I(ip)-18):(I(ip)+18),(J(ip)-18):(J(ip)+18))
         ECL_V10(ip,:,:)=v((I(ip)-18):(I(ip)+18),(J(ip)-18):(J(ip)+18))
         ECL_SLP(ip,:,:)=p((I(ip)-18):(I(ip)+18),(J(ip)-18):(J(ip)+18))
       end if

      delete([/FILE1,FILE2,FILE3,u,v,p, time2/])
  end do

; Finally, write me a netcdf file
ECL_WS10=(ECL_U10^2+ECL_V10^2)^0.5
print(max(ECL_WS10))
copy_VarMeta(ECL_U10,ECL_WS10)
ECL_WS10@description="6-hour instantaneous wind speed within 500km radius of low centre"

fout=OUTDIR+"ECLwindslp_LAP_"+source+"_"+res+"_"+rad+"_proj"+proj+".nc"
system("/bin/rm -f "+fout)
out = addfile(fout,"c")
out->ECL_U10=ECL_U10
out->ECL_V10=ECL_V10
out->ECL_WS10=ECL_WS10
out->ECL_SLP=ECL_SLP
